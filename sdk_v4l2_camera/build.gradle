apply plugin: rootProject.ext.plugins.library

android {
    namespace 'com.hsj.sdk_v4l2_camera'
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    ndkVersion "26.3.11579264"  // 明确指定 NDK 版本

    buildFeatures {
        buildConfig true
    }
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner rootProject.ext.dependencies.testInstrumentationRunner
        externalNativeBuild {
            cmake {
                abiFilters "arm64-v8a"
                cppFlags "-Wunused -frtti -fexceptions -fPIE"
                arguments "-DANDROID_ARM_NEON=TRUE",
                        "-DANDROID_PLATFORM=android-21"
                // 不要在这里设置 CMAKE_BUILD_TYPE，让它根据 buildType 自动选择
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            jniDebuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    // Debug 构建：生成调试符号，优化级别 O0
                    arguments "-DCMAKE_BUILD_TYPE=Debug"
                    cppFlags "-O0", "-g", "-DDEBUG"
                }
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            externalNativeBuild {
                cmake {
                    // Release 构建：优化性能
                    arguments "-DCMAKE_BUILD_TYPE=Release"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    android.libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "sdk_camera_v4l2_v${defaultConfig.versionCode}_${variant.buildType.name}.aar"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation rootProject.ext.dependencies.testJunit
    androidTestImplementation rootProject.ext.dependencies.testExt
    androidTestImplementation rootProject.ext.dependencies.testEspresso
}
